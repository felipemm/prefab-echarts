# Build the project
build:
    uv sync

# Run tests
test: build
    uv run --frozen pytest -xvs tests

# Run ty type checker
typecheck:
    uv run --frozen ty check

# Run all pre-commit checks
lint:
    uv run --frozen prek run --all-files

# Rebuild component previews, CSS, and playground bundle
generate-preview-css:
    uv run tools/render_previews.py
    uv run tools/generate_content.py
    NODE_PATH=renderer/node_modules npx --yes @tailwindcss/cli@4 -i tools/input.css -o /tmp/prefab-preview-raw.css --minify
    uv run tools/scope_css.py
    uv run tools/generate_playground_bundle.py

# Start the renderer dev server
renderer:
    cd renderer && npm run dev

# Serve documentation locally (starts renderer dev server automatically)
docs renderer-port="3333" docs-port="3000":
    uv run prefab dev docs --renderer-port {{renderer-port}} --docs-port {{docs-port}}

# Regenerate playground bundle.json, examples.json, and themes.json
playground:
    uv run tools/generate_playground_bundle.py
    uv run tools/extract_examples.py
    uv run tools/generate_theme_presets.py

# Generate per-component protocol reference pages from Pydantic models
generate-protocol-ref:
    uv run tools/generate_protocol_pages.py

# Check for broken links in documentation
docs-broken-links:
    cd docs && npx --yes mint@latest broken-links
